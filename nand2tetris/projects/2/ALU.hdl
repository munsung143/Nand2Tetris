// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, sel=zx, out=zxOut);
    Not16(in=zxOut, out=nZnOut);
    Mux16(a=zxOut, b=nZnOut, sel=nx, out=nxOut);

    Mux16(a=y, sel=zy, out=zyOut);
    Not16(in=zyOut, out=nZyOut);
    Mux16(a=zyOut, b=nZyOut, sel=ny, out=nyOut);

    Add16(a=nxOut, b=nyOut, out=addOut);
    And16(a=nxOut, b=nyOut, out=andOut);
    Mux16(a=andOut, b=addOut, sel=f, out=comOut);

    Not16(in=comOut, out=nComOut);
    Mux16(a=comOut, b=nComOut, sel=no, out=out, out[0..7]=out0to7, out[8..15]=out8to15, out[15]=ng);
    Or8Way(in=out0to7, out=orOut1);
    Or8Way(in=out8to15, out=orOut2);
    Or(a=orOut1, b=orOut2, out=orTemp);
    Not(in=orTemp, out=zr);

    
}